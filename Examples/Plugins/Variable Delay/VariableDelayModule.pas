unit VariableDelayModule;

////////////////////////////////////////////////////////////////////////////////
//                                                                            //
//  Version: MPL 1.1 or LGPL 2.1 with linking exception                       //
//                                                                            //
//  The contents of this file are subject to the Mozilla Public License       //
//  Version 1.1 (the "License"); you may not use this file except in          //
//  compliance with the License. You may obtain a copy of the License at      //
//  http://www.mozilla.org/MPL/                                               //
//                                                                            //
//  Software distributed under the License is distributed on an "AS IS"       //
//  basis, WITHOUT WARRANTY OF ANY KIND, either express or implied. See the   //
//  License for the specific language governing rights and limitations under  //
//  the License.                                                              //
//                                                                            //
//  Alternatively, the contents of this file may be used under the terms of   //
//  the Free Pascal modified version of the GNU Lesser General Public         //
//  License Version 2.1 (the "FPC modified LGPL License"), in which case the  //
//  provisions of this license are applicable instead of those above.         //
//  Please see the file LICENSE.txt for additional information concerning     //
//  this license.                                                             //
//                                                                            //
//  The code is part of the Delphi ASIO & VST Project                         //
//                                                                            //
//  The initial developer of this code is Christian-W. Budde                  //
//                                                                            //
//  Portions created by Christian-W. Budde are Copyright (C) 2008-2009        //
//  by Christian-W. Budde. All Rights Reserved.                               //
//                                                                            //
////////////////////////////////////////////////////////////////////////////////

interface

{$I DAV_Compiler.inc}

uses
  Windows, Types, SysUtils, Classes, Forms, SyncObjs, DAV_Types,
  DAV_VSTModule, DAV_DspVariableDelay;

type
  TVariableDelayVST = class(TVSTModule)
    procedure VSTModuleCreate(Sender: TObject);
    procedure VSTModuleDestroy(Sender: TObject);
    procedure VSTModuleOpen(Sender: TObject);
    procedure VSTModuleEditOpen(Sender: TObject; var GUI: TForm; ParentWindow: Cardinal);
    procedure VSTModuleSampleRateChange(Sender: TObject; const SampleRate: Single);
    procedure VSTModuleProcess(const Inputs, Outputs: TDAVArrayOfSingleDynArray; const SampleFrames: Integer);
    procedure VSTModuleProcessDoubleReplacing(const Inputs, Outputs: TDAVArrayOfDoubleDynArray; const SampleFrames: Integer);
    procedure SDDelayLengthChange(Sender: TObject; const Index: Integer; var Value: Single);
    procedure ParamDryMixChange(Sender: TObject; const Index: Integer; var Value: Single);
    procedure ParameterWetMixChange(Sender: TObject; const Index: Integer; var Value: Single);
  private
    FCriticalSection : TCriticalSection;
    FVariDelay       : array [0..1] of TCustomVariableDelay32;
    FMix             : array [0..1] of Single;
  end;

implementation

{$R *.DFM}

uses
  VariableDelayGUI, DAV_VSTCustomModule;

procedure TVariableDelayVST.VSTModuleCreate(Sender: TObject);
begin
 FCriticalSection := TCriticalSection.Create;
end;

procedure TVariableDelayVST.VSTModuleDestroy(Sender: TObject);
begin
 FreeAndNil(FCriticalSection);
end;

procedure TVariableDelayVST.VSTModuleOpen(Sender: TObject);
begin
(*
 FVariDelay[0] := TVariableDelay32Hermite.Create; // Hermite
 FVariDelay[1] := TVariableDelay32Linear.Create;  // vs. Linear
*)

 FVariDelay[0] := TVariableDelay32Allpass.Create; // Hermite
 FVariDelay[1] := TVariableDelay32Allpass.Create;  // vs. Linear

 Parameter[0] := 100;
 Parameter[1] := 0;
 Parameter[2] := 100;
end;

procedure TVariableDelayVST.VSTModuleEditOpen(Sender: TObject; var GUI: TForm;
  ParentWindow: Cardinal);
begin
 GUI := TVSTGUI.Create(Self);
end;

procedure TVariableDelayVST.SDDelayLengthChange(Sender: TObject; const Index: Integer; var Value: Single);
var
  Channel : Integer;
begin
 FCriticalSection.Enter;
 try
  for Channel := 0 to Length(FVariDelay) - 1 do
   if assigned(FVariDelay[Channel])
    then FVariDelay[Channel].Delay := 1E-4 * Value;
 finally
   FCriticalSection.Leave;
 end;

 // update GUI
 if EditorForm is TVSTGUI
  then TVSTGUI(EditorForm).UpdateDelayLength;
end;

procedure TVariableDelayVST.ParameterWetMixChange(
  Sender: TObject; const Index: Integer; var Value: Single);
begin
 FMix[1] := 0.01 * Value;

 // update GUI
 if EditorForm is TVSTGUI
  then TVSTGUI(EditorForm).UpdateWetMix;
end;

procedure TVariableDelayVST.ParamDryMixChange(
  Sender: TObject; const Index: Integer; var Value: Single);
begin
 FMix[0] := 0.01 * Value;

 // update GUI
 if EditorForm is TVSTGUI
  then TVSTGUI(EditorForm).UpdateDryMix;
end;

procedure TVariableDelayVST.VSTModuleProcess(const Inputs, Outputs: TDAVArrayOfSingleDynArray; const SampleFrames: Integer);
var
  Sample, Channel : Integer;
begin
 FCriticalSection.Enter;
 try
  for Sample := 0 to SampleFrames - 1 do
   for Channel := 0 to Length(FVariDelay) - 1
    do Outputs[Channel, Sample] := FMix[0] * Inputs[Channel, Sample] +
         FMix[1] * FVariDelay[Channel].ProcessSample32(Inputs[Channel, Sample]);
 finally
  FCriticalSection.Leave;
 end;
end;

procedure TVariableDelayVST.VSTModuleProcessDoubleReplacing(const Inputs,
  Outputs: TDAVArrayOfDoubleDynArray; const SampleFrames: Integer);
var
  Sample, Channel : Integer;
begin
 FCriticalSection.Enter;
 try
  for Sample := 0 to SampleFrames - 1 do
   for Channel := 0 to Length(FVariDelay) - 1
    do Outputs[Channel, Sample] := FMix[0] * Inputs[Channel, Sample] +
         FMix[1] * FVariDelay[Channel].ProcessSample32(Inputs[Channel, Sample]);
 finally
  FCriticalSection.Leave;
 end;
end;

procedure TVariableDelayVST.VSTModuleSampleRateChange(Sender: TObject;
  const SampleRate: Single);
var
  Channel : Integer;
begin
 if Abs(SampleRate) = 0 then Exit;
 
 FCriticalSection.Enter;
 try
  for Channel := 0 to Length(FVariDelay) - 1 do
   if Assigned(FVariDelay[Channel])
    then FVariDelay[Channel].SampleRate := Abs(SampleRate);
 finally
   FCriticalSection.Leave;
 end;
end;

end.
